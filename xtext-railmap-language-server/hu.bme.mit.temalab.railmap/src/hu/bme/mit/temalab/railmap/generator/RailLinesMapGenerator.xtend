/*
 * generated by Xtext 2.12.0
 */
package hu.bme.mit.temalab.railmap.generator

import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.generator.AbstractGenerator
import org.eclipse.xtext.generator.IFileSystemAccess2
import org.eclipse.xtext.generator.IGeneratorContext
import hu.bme.mit.temalab.railmap.railLinesMap.RailLineMap
import hu.bme.mit.temalab.railmap.railLinesMap.Location
import hu.bme.mit.temalab.railmap.railLinesMap.TurnoutEndLocation
import hu.bme.mit.temalab.railmap.railLinesMap.StationTrackLocation

/**
 * Generates code from your model files on save.
 * 
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#code-generation
 */
class RailLinesMapGenerator extends AbstractGenerator {

	override void doGenerate(Resource resource, IFileSystemAccess2 fsa, IGeneratorContext context) {
		resource.contents.filter(RailLineMap).forEach[
			var nextSemaphore = 0;
			fsa.generateFile(mapName + '.dot', '''
				digraph railMap {
					«FOR station: stations»
						«station.name» [label="«station.name» «FOR track: 1..station.tracks»|<t«track»> track «track» «ENDFOR»"][shape=record];
					«ENDFOR»
					«FOR turnout: turnouts»
						«turnout.name» [label="«turnout.name»"][shape=promoter];
					«ENDFOR»
					«FOR connection: connections»
						«connection.src.printLoc»
						«FOR semaphore: 1..connection.length» -> sem«nextSemaphore++»«ENDFOR»
						 -> «connection.dst.printLoc» [label="«connection.gauge.getName»"] 
					«ENDFOR»
				}
			''')
		]		
	}
	
	def printLoc(Location loc) {
		if (loc instanceof TurnoutEndLocation)
			loc.turnout.name
		else if (loc instanceof StationTrackLocation) 
			'''«loc.station.name»:t«loc.track»'''
		else ""
	}

}
